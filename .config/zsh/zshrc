echo "Profile loading from ${ZDOTDIR:=${HOME}}"

setopt extendedglob autocd

#========================#
#      Set Up Paths      #
#========================#

typeset -U homepaths
# collect standard executable directories ... but don't look under the Code or Documents
# directories since they can slow the search down *a lot*
homepaths=(
  '{,.}**/(sbin|bin|alternatives)~*(Code|Documents)*'
)

typeset -U path
path=(
  $(eval realpath ${homepaths/#/${HOME}/} /home/linuxbrew/.linuxbrew/{,Homebrew}/{bin,sbin} 2>/dev/null)
  ${path}
)

#========================#
#        end Paths       #
#========================#


#========================#
# Set Up Lazy Functions  #
#========================#

lazyFuncPath=${ZDOTDIR}/lazyfuncs.d

typeset -U fpath
fpath=(
  ${lazyFuncPath}/**/(F)
  $fpath
)

lazyFuncs=( ${lazyFuncPath}/**/*~*.zwc(.) )

# make sure all the lazy functions are compiled
for lazyFunc in ${lazyFuncs}; do
  if [[ ! -s "${lazyFunc}.zwc" ]]; then
    print "LazyFunc ${lazyFunc} not compiled ... compiling."
    zcompile -UzR "${lazyFunc}"
  fi
done

autoload -UwR ${lazyFuncs}

#========================#
#== End Lazy Functions ==#
#========================#

bindkey -v

#========================#
#== Set Up Completions ==#
#========================#

# TODO: learn more about zstyle/compinstall and generate completions for
# all my autoload funcs
zstyle :compinstall filename '${ZDOTDIR:=${HOME}}/.zshrc'
autoload -Uz compinit
compinit

#========================#
#==  end Completions   ==#
#========================#

#==========================#
#== Source Other Configs ==#
#==========================#

# source all '00-*.zsh' files
for sourceFile in ${ZDOTDIR}/sources.d/**/00-*.zsh; do
  source ${sourceFile}
done

# get all the sources in order, skipping all '00-*.zsh' files (which are executed first)
for sourceFile in ${ZDOTDIR}/sources.d/**/*~(*.zwc|*/00-*)(.); do
  sourceCommand="${${sourceFile:t:r}#*-}"
  [[ -x "${commands[${sourceCommand}]}" ]] && source <(${sourceFile})
done

#==========================#
#==  end Other Configs   ==#
#==========================#

#==========================#
#==    Create Aliases    ==#
#==========================#

# for each alias file, process each line in the ile as an alias definition: aliasName=aliasText
for aliasFile in ${ZDOTDIR}/aliases.d/**/*~*.zwc(.); do
  source <(
    for aliasLine in ${(f)"$(<${aliasFile})"}; do
      printf "alias %s\n" "${aliasLine}"
    done
  )
done

#==========================#
#==     end Aliases      ==#
#==========================#

#=======================#
#==   Run Finalizer   ==#
#=======================#
for finalizer in ${ZDOTDIR}/sources.d/999-*.zsh; do
	source "${finalizer}"
done

#=======================#
#== End Run Finalizer ==#
#=======================#
